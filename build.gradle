plugins {
	id 'org.springframework.boot' version '2.2.6.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
	id 'com.github.node-gradle.node' version '2.2.3'
}

group = 'br.com.cafebinario'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven{
		url 'https://raw.github.com/JimSP/artifacts/master'
	}
}

node {
	download = true
	workDir = file('build/nodejs')
	npmWorkDir = file('build/npm')
}

task appNpmInstall(type: NpmTask) {
	description = 'Installs all dependencies from package.json'
	workingDir = file('src/main/webapp')
	args = ['install']
}

task appNpmBuild(type: NpmTask) {
	description = 'Builds production version of the webapp'
	workingDir = file('src/main/webapp')
	args = ['run', 'build']
}

task copyWebApp(type: Copy) {
	from 'src/main/webapp/build'
	into 'build/resources/main/static/.'
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	
	compile 'br.com.cafebinario:logger-annotation:1.0.0-RELEASE'
	
	runtimeOnly 'com.h2database:h2'
	
	compileOnly 'org.projectlombok:lombok'
	
	annotationProcessor 'org.projectlombok:lombok'
	
	compile 'io.springfox:springfox-swagger-ui:2.9.2'
	compile 'io.springfox:springfox-swagger2:2.9.2'
	
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}

appNpmBuild.dependsOn appNpmInstall
copyWebApp.dependsOn appNpmBuild
compileJava.dependsOn copyWebApp
